*** Settings ***
Documentation    File contains keyword for login into application
Library          Selenium2Library
Resource         ../Settings/Login_Settings.txt

*** Keywords ***
Login To Primat
    [Arguments]    ${validLogin}  ${validPswd}
    Open Login Form
    Insert Credentials And Submit  ${validLogin}  ${validPswd}
    
Open Login Form
    Wait Until Page Contains Element    ${loginLink} 
    Click Element     ${loginLink}
    Verify Login Form Displayed
    
Verify Login Form Displayed
    Wait Until Page Contains  ${authFormTitle} 
    Wait Until Page Contains Element    ${authFormLoginInput}
    Page Should Contain    ${authFormLoginText}    
    Wait Until Page Contains Element    ${authFormPswdInput}     
    Page Should Contain    ${authFormPswdText}    
    Wait Until Page Contains Element    ${authFormSubmitBtn} 
    Page Should Contain    ${authFormBtnText}    

Insert Credentials And Submit
    [Arguments]    ${validLogin}  ${validPswd}
    Input Text    ${authFormLoginInput}     ${validLogin}
    Input Text    ${authFormPswdInput}    ${validPswd}
    Click Element    ${authFormSubmitBtn}
    
Verify Alert Displayed
    Wait Until Page Contains Element    ${alertForm} 
    ### add text verification   
    
Enable Input Validation For Login Without Param Test
    Execute Javascript    document.getElementById('auth_login_form_email').required = true; 
    Execute Javascript    document.getElementById('auth_login_form_password').required = true; 